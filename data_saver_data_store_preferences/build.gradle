plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

//打包main目录下代码和资源的 task
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}


android {
    compileSdk 33
    buildToolsVersion "33.0.0"

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion "1.1.0-rc01"
    }

    publishing {
        singleVariant("release")
    }

}

afterEvaluate {
    publishing {
        def versionName = version_name
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifacts {
                    archives generateSourcesJar
                }

                // You can then customize attributes of the publication as shown below.
                groupId = 'com.funny.compose'
                artifactId = 'data-saver-data-store-preferences'
                version = versionName
            }
        }
        repositories {
            maven {
                // change URLs to point to your repos, e.g. http://my.org/repo
                def baseUrl = buildDir.getParent()
                def releasesRepoUrl = "$baseUrl/repos/releases"
                def snapshotsRepoUrl = "$baseUrl/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {
    def compose_version = "1.1.0-rc01"
    implementation "androidx.compose.ui:ui:$compose_version"

    // kotlin
    def kotlin_version = "1.6.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    implementation project(path: ":data_saver_core")

    // DataStore
    def data_store_version = "1.0.0"
    implementation "androidx.datastore:datastore:$data_store_version"
    implementation "androidx.datastore:datastore-preferences:$data_store_version"

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}